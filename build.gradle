buildscript {
	repositories {
		gradlePluginPortal()
		maven {
			url "${System.getenv("ARTIFACTORY_URL")}/artifactory/maven-dev-local/"
		}
	}
	dependencies {
		classpath("altima-api:plugin-gradle:1.3.5+")
	}
}

plugins {
	id 'org.springframework.boot' version '2.1.3.RELEASE'
	id 'org.jetbrains.kotlin.jvm' version '1.3.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21'
	id "com.moowork.node" version "1.3.1"
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'altima.build.defaults'
apply plugin: 'altima.build.api'

altimaBuild {
	kotlin = true
}

altimaApi {
	kotlin = true
}

dependencies {
	compile platform('altima-api:dependencies:3.1.0')
	compile('altima-api:sugar:5.1.0')
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-mustache'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    runtimeOnly("org.springframework.boot:spring-boot-devtools")
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
	}
}

// Task for installing frontend dependencies in web
task installDependencies(type: YarnTask) {
	args = ['install']
	execOverrides {
		it.workingDir = './react'
	}
}

// Task for executing build:gradle in web
task buildWeb(type: YarnTask) {
	args = ['build']
	execOverrides {
		it.workingDir = './react'
	}
}

// Before buildWeb can run, installDependencies must run
buildWeb.dependsOn installDependencies

// Before build can run, buildWeb must run
build.dependsOn buildWeb
